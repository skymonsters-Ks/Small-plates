;$a>gamtest.ax

;$data wx:480,wy:800,mem:16
#define TITLE_NAME         "Gyro-Accel-MultiTouch Test"
#define DATA_FONT          "font/recp8x8"
#define DATA_FONTIMG       "font/recp8x8.png"
;$end

#include "hsp3dish.as"
#include "../common/platform.hsp"
#include "../common/imgfont.hsp"

	initImgFont
	loadImgFont cnvName(DATA_FONT), cnvName(DATA_FONTIMG)
	setImgFontAttr 2, 2, 3
	
	setcls 1, $5588cc
	
	gmode 3,,,255
	
	cmax = 16
	dim ce, cmax
	dim cx, cmax
	dim cy, cmax
	dim cs, cmax
	
	ptnum = 0
	
*mainLoop

	mtlist mtl
	tnum = stat
	
	if (tnum > ptnum) {
		repeat tnum - ptnum
			i = cnt
			repeat cmax
				if (ce(cnt) == 0) {
					ce(cnt) = 1
					mtinfo v, mtl(ptnum + i)
					cx(cnt) = v(1)
					cy(cnt) = v(2)
					cs(cnt) = 70
					break
				}
			loop
		loop
	}
	ptnum = tnum
	
	repeat cmax
		if (ce(cnt)) {
			cs(cnt) += 15
			if (cs(cnt) > 600) {
				ce(cnt) = 0
			}
		}
	loop
	
	redraw 0

	repeat tnum
		mtinfo v, mtl(cnt)
		x = v(1)
		y = v(2)
		s = 80
		color 250, 190, 230
		circle x - s, y - s, x + s, y + s
	loop
	
	repeat cmax
		if (ce(cnt)) {
			x = cx(cnt)
			y = cy(cnt)
			s = cs(cnt)
			color 250, 190, 230
			circle x - s, y - s, x + s, y + s, 0
		}
	loop
	
	drawImgStr strf("GyroX: %.3f", ginfo(259)), 10, 10
	drawImgStr strf("GyroY: %.3f", ginfo(260)), 10, 30
	drawImgStr strf("GyroZ: %.3f", ginfo(261)), 10, 50
	drawImgStr strf("AccelX: %.3f", ginfo(256)), 10, 80
	drawImgStr strf("AccelY: %.3f", ginfo(257)), 10, 100
	drawImgStr strf("AccelZ: %.3f", ginfo(258)), 10, 120
	drawImgStr strf("MouseX: %d", mousex), 10, 150
	drawImgStr strf("MouseY: %d", mousey), 10, 170
	
	repeat tnum
		mtinfo v, mtl(cnt)
		drawImgStr strf("Touch%d: (%d, %d)", cnt, v(1), v(2)), 10, 200 + 20 * cnt
	loop
	
	redraw 1
	
	await 15
	
	goto *mainLoop
	