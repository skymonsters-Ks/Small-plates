#packopt name "genhtml"

#include "hsp3utf.as"

#uselib "shell32"
#func dragAcceptFiles "DragAcceptFiles" int, int
#func dragQueryFile   "DragQueryFileW" int, int, wptr, int
#func dragFinish      "DragFinish" int

	title "genHtml"
	oncmd gosub *evDropFiles, $233  ; WM_DROPFILES
	dragAcceptFiles hwnd, 1
	syscolor 15 : boxf
	objsize 160
	pos 10, 0
	chkbox "Export package on desktop", package
	sdim buf, 32000
	buf = "drop folders here"
	pos 0, 24
	mesbox buf, 640, 456, 0
	iBox = stat
	sdim fn, 1024
	envstr = "wx",  "wy",  "rate", "step", "mem", "cap"
	envdef = "640", "480", "1",    "5000", "32",  "0"
	stop

#deffunc mesError str _s
	buf += "**ERROR** " + _s + "\n"
	return

*evDropFiles
	hDrop = wparam
	dragQueryFile hDrop, -1, 0, 0
	num = stat
	buf = ""
	repeat num
		sdim ts, 512
		dragQueryFile hDrop, cnt, varptr(ts), 512
		dir = cnvwtos(ts)
		dirlist x, dir, 5
		if (stat) {
			sdim fd
			notesel fd
			gosub *genHtml
			noteunsel
		} else {
			mesError dir + " not exist"
		}
	loop
	dragFinish hDrop
	objprm iBox, buf
	return

*genHtml
	if (package) {
		split dir, "\\", res
		outdir = dir_desktop + "\\" + res(stat - 1)
		dirlist x, outdir, 5
		if (stat) {
			mesError outdir + " exist"
			return
		}
		tempHtml = "temp_simple.html"
	} else {
		tempHtml = "temp.html"
	}
	
	sdim axName
	sdim envData
	sdim titleName, 512 : titleName = "''"
	sdim dataName, 4000
	sdim bodyTag, 10000 : bodyTag = "''"
	sdim style, 10000
	
	fn = dir + "\\.cmpexdef"
	exist fn
	if (strsize > 0) {
		noteload fn
		idx = instr(fd, 0, ";$a>")
		if (idx < 0) : mesError dir + " ;$a> not exist" : return
		getstr ts, fd, idx + 4
		axName = strtrim(ts, 0, ' ')
	}
	
	fn = dir + "\\main.hsp"
	exist fn
	if (strsize <= 0) : mesError fn + " not exist" : return
	noteload fn
	
	if (axName == "") {
		idx = instr(fd, 0, ";$a>")
		if (idx < 0) : mesError dir + " ;$a> not exist" : return
		getstr ts, fd, idx + 4
		axName = strtrim(ts, 0, ' ')
	}
	
	idx = instr(fd, 0, ";$data")
	if (idx < 0) : mesError dir + " ;$data not exist" : return
	getstr ts, fd, idx + 6
	idx += strsize + 6
	envData = strtrim(ts)
	foreach envstr
		i = instr(envData, 0, envstr(cnt))
		if (i < 0) : envData += strf(",%s:%s", envstr(cnt), envdef(cnt))
	loop
	envData = strtrim(envData, 1, ',')
	getstr wx, envData, instr(envData, 0, "wx") + 3, ','
	getstr wy, envData, instr(envData, 0, "wy") + 3, ','
	getstr rate, envData, instr(envData, 0, "rate") + 5, ','
	cvsWidth = int(wx) * int(rate)
	cvsHeight = int(wy) * int(rate)
	
	idxEnd = instr(fd, idx, ";$end")
	if (idxEnd < 0) : mesError dir + " ;$end not exist" : return
	idxEnd += idx
	
	repeat
		if (idx >= idxEnd) : break
		getstr ts, fd, idx
		idx += strsize
		i = instr(ts, 0, "TITLE_NAME")
		if (i >= 0) {
			i += instr(ts, i, "\"") + 1
			getstr ts, ts, i, '\"'
			titleName = ts
			continue
		}
		i = instr(ts, 0, "DATA_")
		if (i >= 0) {
			i += instr(ts, i, "\"") + 1
			getstr ts, ts, i, '\"'
			dataName += "'" + ts + "',"
			continue
		}
		i = instr(ts, 0, ";$body ")
		if (i >= 0) {
			getstr ts, ts, i + 7
			bodyTag = "'" + ts + "'"
			continue
		}
		i = instr(ts, 0, ";$style ")
		if (i >= 0) {
			getstr ts, ts, i + 8
			style = ts
			continue
		}
	loop
	dataName = "'ax/" + axName + "'," + dataName
	dataName = strtrim(dataName, 0, ',')
	
	noteload tempHtml
	strrep fd, "$$titleName$$", titleName
	strrep fd, "$$envData$$", envData
	strrep fd, "$$dataName$$", dataName
	strrep fd, "$$bodyTag$$", bodyTag
	strrep fd, "$$style$$", style
	strrep fd, "$$canvasWidth$$", str(cvsWidth)
	strrep fd, "$$canvasHeight$$", str(cvsHeight)
	strrep fd, "$$viewWidth$$", str(cvsWidth + (package ^ 1) * 120)
	
	if (package) {
		q = "'"
		ds = "js", "data"
		sdim res, 64, 64
		split dataName, ",", res
		; 必要なフォルダ・リソースをチェック
		resnum = stat
		repeat resnum
			res(cnt) = strtrim(res(cnt), 0, peek(q))
			i = instr(res(cnt), 0, "/")
			if (i >= 1) {
				getstr d, res(cnt), 0, '/'
				ds(length(ds)) = "data/" + d ; 被りは後でチェックする
				res(cnt) = "data/" + res(cnt)
			}
		loop
		res(resnum) = "js/load.js", "js/hsp3dish.js"
		resnum += 2
		; フォルダ作成
		mkdir outdir
		foreach ds
			d = outdir + "/" + ds(cnt)
			dirlist x, d, 5
			if (stat == 0) { ; 被りチェック
				mkdir d
			}
		loop
		; リソースコピー
		repeat resnum
			s = "../docs/" + res(cnt)
			d = outdir + "/" + res(cnt)
			bcopy s, d
		loop
		; index.html 作成
		notesave outdir + "/index.html"
		buf += "exported " + outdir + "\n"
	} else {
		fn = "../docs/x_" + getpath(axName, 1) + ".html"
		notesave fn
		buf += "saved as " + fn + "\n"
	}

	return
