#module stage

#deffunc initStage

	randomize
	time = 0.0
	rank = 1.0
	apcnt = 50
	stdelay = getDelay()
	return
	
#deffunc runStage

	delay = getDelay() - stdelay
	time += 16.666666
	rank += 0.001
	emax = limit(4 + time / 15000, 4, getEnemyMax())
	apcnt--
	if (apcnt == 0) {
		apcnt = limit(90.0 - rank * 5 - sin(time / 6500) * 10, 10, 80)
		en = getEnemyNum()
		if (en < emax) {
			cl = limit(emax - 7, 0, 4)
			if (en <= cl) {
				ptnCircle emax - cl
			} else {
				ptnSingle
			}
		}
	}
	return

#defcfunc getRank

	return rank

#deffunc ptnSingle@stage

	repeat
		a = 0.01 * rnd(628)
		r = rnd(200)
		x = cos(a) * r + 240
		y = sin(a) * r + 240
		if (distPlayer(x, y) > limit(100 - getJumpCounter(), 60, 100)) : if (distNearestEnemy(x, y) > 40) {
			genEnemy x, y
			break
		}
	loop
	return

#deffunc ptnCircle@stage int _d

	a = 0.01 * rnd(628)
	pd = M_PI * 2 / _d
	repeat _d
		t = pd * cnt + a
		x = cos(t) * 200 + 240
		y = sin(t) * 200 + 240
		if (distPlayer(x, y) > 40) : if (distNearestEnemy(x, y) > 40) {
			genEnemy x, y
		}
	loop
	return

#deffunc drawTime

	gmulcolor
	if (delay >= 100) {
		setImgFontAttr 1, 1, 2
		drawImgStrR strf("%.1f", double(delay) / 1000), 470, 442
	}
	setImgFontAttr 2, 2, 3
	if (time >= 60000) {
		drawImgStrR strf("%d:%02d.%d", time / 60000, (time / 1000) \ 60, time \ 1000 / 100), 470, 454
	} else {
		drawImgStrR strf("%d.%d", (time / 1000) \ 60, time \ 1000 / 100), 470, 454
	}
	return

#global
