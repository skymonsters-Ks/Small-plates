#module stage

#deffunc initStage

	randomize
	time = 0.0
	rank = 1.0
	apcnt = 50
	return
	
#deffunc runStage

	time += 16.666666
	rank += 0.001
	emax = limit(4 + rank * 0.8, 4, getEnemyMax())
	apcnt--
	if (apcnt == 0) {
		apcnt = limit(90.0 - rank * 4.0, 10, 80)
		en = getEnemyNum()
		if (en >= emax) : return
		cl = limit(emax - 9, 0, 4)
		if (en <= cl) {
			ptnCircle emax - cl
		} else {
			ptnSingle
		}
	}
	return

#defcfunc getRank

	return rank

#deffunc ptnSingle@stage

	repeat
		a = 0.01 * rnd(628)
		r = rnd(200)
		x = cos(a) * r + 240
		y = sin(a) * r + 240
		if (distPlayer(x, y) > 50) : if (distNearestEnemy(x, y) > 40) {
			genEnemy x, y
			break
		}
	loop
	return

#deffunc ptnCircle@stage int _d

	a = 0.01 * rnd(628)
	pd = M_PI * 2 / _d
	repeat _d
		t = pd * cnt + a
		x = cos(t) * 200 + 240
		y = sin(t) * 200 + 240
		if (distPlayer(x, y) > 30) {
			genEnemy x, y
		}
	loop
	return

#deffunc drawTime

	gmulcolor
	setImgFontAttr 2, 2, 3
	if (time >= 60000) {
		drawImgStrR strf("%d:%02d.%d", time / 60000, (time / 1000) \ 60, time \ 1000 / 100), 470, 454
	} else {
		drawImgStrR strf("%d.%d", (time / 1000) \ 60, time \ 1000 / 100), 470, 454
	}
	return

#global
