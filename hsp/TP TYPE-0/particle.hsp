#module particle

#deffunc initParticle

	num = 100
	dim life, num
	dim tcnt, num
	ddim posx, num
	ddim posy, num
	ddim velx, num
	ddim vely, num
	ddim angl, num
	return

#deffunc genParticle double _x, double _y

	repeat num
		if (life(cnt)) : else {
			life(cnt) = rnd(20) + 10
			tcnt(cnt) = 0
			posx(cnt) = _x
			posy(cnt) = _y
			a = 0.01 * rnd(629)
			s = 0.01 * rnd(800) + 3
			velx(cnt) = cos(a) * s
			vely(cnt) = sin(a) * s
			angl(cnt) = a
			break
		}
	loop
	return

#deffunc moveParticle

	repeat num : i = cnt
		if (life(i)) {
			if (inArea(posx(i), posy(i)) == 0) {
				life(i) = 0
				continue
			}
			posx(i) += velx(i)
			posy(i) += vely(i)
			velx(i) *= 0.95
			vely(i) *= 0.95
			tcnt(i)++
			life(i)--
		}
	loop
	return

#deffunc drawParticle

	color 255, 255, 255
	repeat num
		if (life(cnt)) {
			s = 0.3 * life(cnt) + 1
			grect posx(cnt), posy(cnt), angl(cnt), s, s
		}
	loop
	return

#global
