#module

#define global setFrameTime(%1=16.666666) setFrameTime_ %1

#deffunc initFrame

	dim dtime, 2
	setFrameTime
	resetFrame
	return

#deffunc resetFrame

	ucTotal = 0
	delay = 0
	getreq stime, SYSREQ_TIMER
	return
	
#deffunc setFrameTime_ double _ms

	if (_ms > 0) : msecPerFrame = _ms
	return
	
#deffunc setFrameRoutin label _rtUpdate, label _rtDraw, int _mc

	rtUpdate = _rtUpdate
	rtDraw = _rtDraw
	ucMax = _mc
	return
	
#deffunc runFrame

	uc = 0
	getreq time, SYSREQ_TIMER
	dt = double(time - stime) - msecPerFrame * ucTotal
	if (dt >= 0) {
		uc++
		ucTotal++
		dc = dt / msecPerFrame
		if (dc >= ucMax) {
			ucTotal += ucMax
			uc += ucMax
			t = msecPerFrame * (dc - ucMax)
			delay += t
			stime += t
		} else : if (dc > 0) {
			ucTotal += dc
			uc += dc
		}
		getreq t1, SYSREQ_TIMER
		repeat uc
			gosub rtUpdate
		loop
		getreq t2, SYSREQ_TIMER
		redraw 0
		gosub rtDraw
		redraw 1
		getreq t3, SYSREQ_TIMER
		dtime = t2 - t1, t3 - t2
	}
	await 1
	return dt

#defcfunc getUpdateDt
	return dtime
	
#defcfunc getDrawDt
	return dtime(1)

#defcfunc getDelay
	return delay

#global
